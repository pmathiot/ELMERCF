!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Compute the viscosity as Visc=prefactor^2 * Mu
! .sif usage: SSA Mean Viscosity = Variable "prefactor", "Mu"
! where prefactor is a dimensionless prefactor used to scale the initial
!  viscosity field "Mu"
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
       FUNCTION SSAViscosity(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn(2)
       REAL(kind=dp) :: VarOut
      
       REAL(kind=dp) :: prefactor,mu
     
         prefactor=VarIn(1)
         mu=VarIn(2)
         
         VarOut=prefactor*prefactor*mu

       End
! Derivative of the function above w.r.t. prefactor
       FUNCTION SSAViscosity_d(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn(2)
       REAL(kind=dp) :: VarOut
      
       REAL(kind=dp) :: prefactor,mu
     
         prefactor=VarIn(1)
         mu=VarIn(2)
         
         VarOut=2.0*prefactor*mu

       End
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  A USF that can be used with SaveScalars to get the grounded area;
!   tested in 2D plane view applications with SSA
!
!  SaveScalars:
!      Variable 5 = "groundedmask"
!      Operator 5 = "volume"
!      Coefficient 5 = "GroundedAreaCoeff"
!  Material:
!       GroundedAreaCoeff = Variable groundedmask
!         real procedure "USF_Ronnie_inversion" "GroundedAreaCoeff" 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
       FUNCTION GroundedAreaCoeff(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn,VarOut

       TYPE(Element_t),POINTER :: Element
       REAL(KIND=dp), ALLOCATABLE,SAVE :: LocalGL(:)
       LOGICAL,SAVE:: AllocationDone=.FALSE.
       INTEGER :: n

       IF (.NOT.AllocationDone) THEN 
         allocate(LocalGL(Model % Mesh % MaxElementNodes))
         AllocationDone=.TRUE.
       END IF

       Element => GetCurrentElement()
       n=Element%TYPE%NumberOfNodes
       CALL GetLocalSolution(LocalGL,'groundedmask',Element)
       IF (ALL(LocalGL(1:n).GT.-0.5)) THEN
          VarOut=1._dp
       ELSE
          VarOut=0._dp
       END IF

       End FUNCTION GroundedAreaCoeff


!! define le max value between the input and the critical H given
       FUNCTION HMin(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn,VarOut
       REAL(kind=dp) :: Hc
       LOGICAL :: Found

       Hc = GetCReal( Model % Constants, 'HMin Critical Thickness', Found )
       IF (.NOT.Found) &
          CALL FATAL('HMin','<HMin Critical Thickness> not found')

       VarOut = MAX(VarIn,Hc)

       End FUNCTION HMin
      
!! input greater than -0.5 give value -1 else give 1
       FUNCTION PassiveCond_DJDBReg(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn,VarOut

       IF (VarIn.GT.-0.5) THEN
          VarOut=-1.0
       ELSE
          VarOut=+1.0
       ENDIF

       END FUNCTION PassiveCond_DJDBReg

!! Compute initial Friction value as C=rho*g*(zs-zb)*alpha/||u||^m
      FUNCTION Log10CIni(Model,nodenumber,VarIn) RESULT(VarOut)

       USE DefUtils

       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn(6) !zs,zb,ds/dx,ds/dy,ux,uy
       REAL(kind=dp) :: VarOut

       TYPE(ValueList_t), POINTER :: Material,BodyForce
       REAL(kind=dp) :: rho,g,H,alpha,u,fm
       REAL(kind=dp) :: C
       REAL(kind=dp),parameter :: CMin=1.0d-6,CMax=1.0_dp,CNoVal=1.0d-1
       REAL(kind=dp),parameter :: HMin=1.0d-1,UMin=1.0_dp
       LOGICAL :: Found
       CHARACTER(LEN=MAX_NAME_LEN) :: SolverName='CInit'
       CHARACTER(LEN=MAX_NAME_LEN) :: Friction


       Material => GetMaterial()
       IF (.NOT.ASSOCIATED(Material)) CALL FATAL(SolverName,&
              'Material not ASSOCIATED')
       BodyForce => GetBodyForce()
       IF (.NOT.ASSOCIATED(BodyForce)) CALL FATAL(SolverName,&
              'BodyForce not ASSOCIATED')

     ! get density
       rho=ListGetRealAtNode(Material, 'SSA Mean Density',nodenumber,Found)
       IF (.NOT.Found) &
           CALL FATAL(SolverName,&
                'Could not find Material prop.  >SSA Mean Density<')

     !get gravity
       g=ListGetRealAtNode(BodyForce,'Flow BodyForce 3',nodenumber,Found)
       IF (.NOT.Found) &
           CALL FATAL(SolverName,&
                'Could not find Body Force >Flow BodyForce 3<')

     IF (VarIn(1).LT.-0.5) THEN
        C=CMin
     ELSE

     ! H=zs-zb
       H=VarIn(2)

     !slope
       alpha=sqrt(VarIn(3)*VarIn(3)+VarIn(4)*VarIn(4))

     ! velocity
       u=sqrt(VarIn(5)*VarIn(5)+VarIn(6)*VarIn(6))

     ! get friction exponent
        Friction = GetString(Material, 'SSA Friction Law', Found)
        IF (.NOT.Found) &
           CALL FATAL(SolverName,&
                'Could not find Material keyword >SSA Friction Law<')

      SELECT CASE(Friction)
       CASE('linear')
           fm = 1.0_dp
       CASE('weertman')
           fm = ListGetConstReal( Material, 'SSA Friction Exponent', Found )
           IF (.NOT.Found) &
               CALL FATAL(SolverName,&
                'Could not find Material prop. >SSA Friction Exponent<')
       CASE DEFAULT
         CALL FATAL(SolverName,'Friction should be linear or Weertman')
      END SELECT

      ! Compute C
      IF ((H.LT.Hmin).OR.(u.LT.Umin)) THEN
          C=CNoVal
      ELSE
          C=abs(rho*g)*H*alpha/u**fm
      END IF

      ENDIF

       VarOut=Log10(min(max(CMin,C),CMax))

       End FUNCTION Log10CIni

!! reverse the sign of the value
       FUNCTION MinusA(Model,nodenumber,VarIn) RESULT(VarOut)
       USE DefUtils
       implicit none
       !-----------------
       TYPE(Model_t) :: Model
       INTEGER :: nodenumber
       REAL(kind=dp) :: VarIn,VarOut

       VarOut = -VarIn

       End FUNCTION MinusA

